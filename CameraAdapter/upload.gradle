apply plugin: 'maven'

def getDefaultGroupId(p) {
  def segments = []

  for (; p; p = p.parent) {
    segments.push(p.name)
  }

  return segments.reverse().join('.')
}

uploadArchives {
  repositories {
    mavenDeployer {
      pom.project {
        groupId project.GROUP_ID
        artifactId project.ARTIFACT_ID
        version project.VERSION
      }

      // parentGroupId is parent group id
      def parentGroupId = getDefaultGroupId(project.parent)

      pom.withXml {
        it.asNode().dependencies.dependency.findAll { it.groupId.text() == parentGroupId }.each {
          def p = project.parent.childProjects[it.artifactId.text()]

          //  Sync all module local dependency to its latest artifact info
          it.groupId.each {
            it.value = p.GROUP_ID
          }
          it.artifactId.each {
            it.value = p.ARTIFACT_ID
          }
          it.version.each {
            it.value = sdkVersion
          }
        }
      }

      repository(url: project.RELEASE_REPOSITORY_URL) {
        authentication(userName: project.USERNAME, password: project.PASSWORD)
      }
      snapshotRepository(url: project.SNAPSHOT_REPOSITORY_URL) {
        authentication(userName: project.USERNAME, password: project.PASSWORD)
      }


    }
  }
}